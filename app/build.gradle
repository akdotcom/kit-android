apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 19
        versionCode 14
        versionName "1.1.0"
    }
    signingConfigs {
        release {
            storeFile file("lastinitial-key.keystore")
            storePassword "FAKE_PASSWORD"
            keyAlias "lastinitial"
            keyPassword "FAKE_PASSWORD"
        }
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
        debug {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            applicationIdSuffix ".debug"
            versionNameSuffix " debug"
        }
    }
}

dependencies {
    compile 'com.google.android.gms:play-services:4.4.52'
    compile "com.mixpanel.android:mixpanel-android:4.3.1@aar"
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task askForPasswords << {
    // Must create String because System.readPassword() returns char[]
    // (and assigning that below fails silently)
    def storePw = new String(System.console().readPassword("\nKeystore password: "))
    def keyPw  = new String(System.console().readPassword("\nKey password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}